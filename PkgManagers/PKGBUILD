# Maintainer: Kimiblock Moe
pkgname=clashtui-git
pkgdesc="Mihomo TUI Client"
url="https://github.com/JohanChane/clashtui"
license=("MIT")
arch=("any")
pkgver=r4.a1e9deb
pkgrel=1
makedepends=("rust" "cargo" "git")
depends=("openssl")
source=("git+https://github.com/JohanChane/clashtui.git#branch=dev")
md5sums=("SKIP")
provides=("clashtui")

function pkgver() {
  cd "${srcdir}/clashtui/clashtui"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

function prepare() {
  cd "${srcdir}/clashtui/clashtui"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --target "$CARCH-unknown-linux-gnu"
}

function build() {
  cd "${srcdir}/clashtui/clashtui"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --release --all-features --locked --frozen
}

function check() {
  cd "${srcdir}/clashtui/clashtui"
	export RUSTUP_TOOLCHAIN=stable
	cargo test --release --all-features --locked --frozen
}

function package() {
	install -Dm755 "${srcdir}/clashtui/clashtui/target/release/clashtui" "${pkgdir}/usr/bin/clashtui"
}
